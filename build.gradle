plugins {
    id "java"
    id "maven-publish"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
compileJava.options.encoding = "UTF-8"

archivesBaseName = project.maven_name
group = project.maven_group
version = project.maven_version

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = "https://repo.papermc.io/repository/maven-public/"
        name = "papermc-repo"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-server:1.19.4-R0.1-SNAPSHOT:mojang-mapped"
    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    failOnError = false
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = ((project.updatable == "true") ? "https://maven.lenni0451.net/releases" : "https://maven.lenni0451.net/snapshots")
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = project.maven_name
            groupId = project.maven_group
            version = project.maven_version

            from components.java
        }
    }
}

processResources {
    inputs.properties(
            "version": project.maven_version + ((project.updatable == "false") ? "-SNAPSHOT" : "")
    )

    filesMatching("plugin.yml") {
        expand(
                "version": project.maven_version + ((project.updatable == "false") ? "-SNAPSHOT" : "")
        )
    }
}
